
plugins {
    id 'java-library'
    id 'maven-publish'
}

repositories {
    ivy {
        layout 'pattern' , {
            artifact '[organisation]/Releases/Packages/[module]/[revision]/[artifact]-[revision].[ext]'
            ivy '[organisation]/Releases/Packages/[module]/[revision]/ivy.xml'
        }
        url = uri('https://svn.win.tue.nl/repos')
        content {
            includeGroup "prom"
        }
    }
    ivy {
        layout 'pattern' , {
            artifact '[module]/[revision]/[artifact]-[revision].[ext]'
            ivy '[module]/[revision]/ivy.xml'
        }
        url = uri('https://svn.win.tue.nl/repos/prom/Libraries')
    }
    mavenCentral()
}

dependencies {
    // Latest is implementation 'prom:StochasticPetriNets:6.10.170'
    // but resolving that in the prom repo is not working, probably because 
    // they are zip files not in a specific revision directory
    api 'prom:StochasticPetriNets:latest'
    api 'org.apache.logging.log4j:log4j-api:2.8.2' 
    api 'org.apache.logging.log4j:log4j-core:2.8.2'
    api 'org.simpleframework:simple-xml:2.7.1'
    testImplementation 'junit:junit:4.11'
}

java{
    withSourcesJar()
}

group = 'qut.pm'
version = '0.8.5-SNAPSHOT'
description = 'prom-helpers'
sourceCompatibility = '1.8'

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/adamburkegh/prom-helpers")
            credentials {
                username = "adamburkegh"
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

